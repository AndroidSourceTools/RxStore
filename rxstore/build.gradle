/*
 * Copyright (C) GRIDSTONE 2015
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath rootProject.ext.gradleExtraConfig
  }
}

plugins {
  id 'com.jfrog.bintray' version '1.3.1'
}

apply plugin: 'optional-base'
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7

dependencies {
  compile rootProject.ext.rxJava

  testCompile rootProject.ext.junit
  testCompile rootProject.ext.assertj
  testCompile rootProject.ext.mockito

  compile group: 'com.google.android', name: 'android', version: '+', optional
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

publishing {
  publications {
    RxStore(MavenPublication) {
      from components.java
      groupId GROUP
      artifactId 'rxstore'
      version VERSION_NAME
      artifact sourcesJar
      artifact javadocJar
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['RxStore']
  pkg {
    repo = 'RxStore'
    name = 'rxstore'
    userOrg = ORGANISATION
    licenses = [LICENSE]
    desc = DESCRIPTION
    websiteUrl = PROJECT_URL
    issueTrackerUrl = ISSUE_TRACKER_URL
    vcsUrl = PROJECT_URL
  }
}
