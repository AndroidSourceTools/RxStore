/*
 * Copyright (C) GRIDSTONE 2018
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath rootProject.ext.kotlinPlugin
    classpath rootProject.ext.bintrayPlugin
  }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
  api rootProject.ext.rxJava

  testImplementation rootProject.ext.junit
  testImplementation rootProject.ext.truth
  testImplementation rootProject.ext.kotlinStdlib
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

publishing {
  publications {
    RxStore(MavenPublication) {
      from components.java
      groupId GROUP
      artifactId 'rxstore'
      version VERSION_NAME
      artifact sourcesJar
      artifact javadocJar

      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          name 'RxStore'
          description DESCRIPTION
          url PROJECT_URL
          developers {
            developer {
              id POM_DEVELOPER_ID
              name POM_DEVELOPER_NAME
            }
          }
          licenses {
            license {
              name POM_LICENCE_NAME
              url POM_LICENCE_URL
              distribution POM_LICENCE_DIST
            }
          }
          scm {
            url PROJECT_URL
            connection POM_SCM_CONNECTION
            developerConnection POM_SCM_DEV_CONNECTION
          }
        }
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['RxStore']
  pkg {
    repo = BINTRAY_REPO
    name = BINTRAY_NAME
    userOrg = ORGANISATION
    licenses = [LICENSE]
    desc = DESCRIPTION
    websiteUrl = PROJECT_URL
    issueTrackerUrl = ISSUE_TRACKER_URL
    vcsUrl = PROJECT_URL
  }
}
